begin
{
                    # if (INT) go mrti
                    (A0=0, B=0, C=1, MADDR=mrti),

    fetch:          (T2, C0),
                    (TA, R, BW=11, M1=1, C1=1),
                    (M2, C2, T1, C3),
                    (A0, B=0, C=0),

    mrti:           # MBR <- DB <- INTV
                    (INTA, BW=11, M1=1, C1=1),
                    # RT1 <- MBR
                    (T1=1, C4=1),

    csw_rt1:        # notify: The event handler microsubrutine.
                    # notify: First, save the current PC+SR:
                    # notify: <font color=blue>push PC</font>
                    # notify: <font color=blue>push SR</font>
                    # notify: Then get the memory address to the assembly subrutine associated with RT1:
                    # notify: <font color=blue>MAR = RT1*4</font>
                    # notify: <font color=blue>MBR = MP[MAR]</font>
                    # notify: <font color=blue>PC  = MAR</font>
                    # notify: Finally, go fetch to start executing the assembly subrutine associated with RT1
                    # notify: <font color=blue>go fetch</font>

                    # push PC
                    (MR=1, SELA=11101, MA=0, MB=10, MC=1, SELCOP=1011, T6=1, SELC=11101, LC=1, C0),
                    (T2=1, M1=0, C1),
                    (BW=11, TA=1, TD=1, W=1)
                    # push SR
                    (MR=1, SELA=11101, MA=0, MB=10, MC=1, SELCOP=1011, T6=1, SELC=11101, LC=1, C0),
                    (T8=1, M1=0, C1),
                    (BW=11, TA=1, TD=1, W=1),
                    # MAR <- RT1*4
                    (MA=1, MB=10, MC=1, SELCOP=1100, T6, M2=0, C0),
                    # MBR <- MP[MAR]
                    (TA=1, R=1, BW=11, M1=1, C1=1),
                    # PC <- MAR
                    (T1, M2=0, C2),
                    # go fetch
                    (A0=0, B=1, C=0, MADDR=fetch)
}


#
# INT
#

reti {
        co=111110,
        nwords=1,
        {
              # notify: The RETI instruction.
              # notify: After the event is handled (interruption, exception or system call)
              # notify: the execution of the current assemble program may stop (e.g.: exception because error)
              # notify: or may continue (i.e.: correct print system call).
              # notify: In order to continue, the state saved in the event handler microsubrutine 
              # notify: must to be restored, and the reti instruction take care of this.
              # notify: 
              # notify: It restore the SR and PC register to continue the interrupted assembly program:
              # notify: <font color=blue>pop SR</font>
              # notify: <font color=blue>pop PC</font>
              # notify: Then go fetch, to keep executing the assembly code as no event has happened:
              # notify: <font color=blue>go fetch</font>

              # pop SR
              (MR=1, SELA=11101, T9, C0),
              (MR=1, SELA=11101, MA=0, MB=10, MC=1, SELCOP=1010, T6=1, SELC=11101, LC=1),
              (TA=1, R=1, BW=11, M1=1, C1),
              (T1=1, M7=0, C7),
              # pop PC + go fetch
              (MR=1, SELA=11101, T9, C0),
              (MR=1, SELA=11101, MA=0, MB=10, MC=1, SELCOP=1010, T6=1, SELC=11101, LC=1),
              (TA=1, R=1, BW=11, M1=1, C1),
              (T1=1, M2=0, C2, A0=1, B=1 ,C=0)
        }
}

syscall {
        co=101110,
        nwords=1,
        {
                    # notify: The SYSCALL instruction.
                    # notify: The syscall instruction has an associated event -> The System Call Event.
                    # notify: It is used to request some service to the operating system.
                    # notify: The operating system provides the associated assembly handler code
                    # notify: that reads from registers/stacks the parameters of the syscall 
                    # notify: and execute the proper actions.
                    # notify: 
                    # notify: The syscall microcode sets the associated event identification in RT1:
                    # notify: <font color=blue>RT1 = 2</font>
                    # notify: Then, microjump to event handler microsubrutine:
                    # notify: <font color=blue>go csw_rti</font>
                    # notify: 
                    # notify: Rembember: PC already points to next instructions after syscall

                    # RT1 <- ExCode=2
                    (ExCode=10, T11, C4),
                    # csw_rt1(2)
                    (A0=0, B=1, C=0, MADDR=csw_rt1)
        }
}


#
# ALU
#

and reg1 reg2 reg3 {
            co=000000,
            cop=0000,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                (MC=1, MR=0, SELA=1011, SELB=10000, MA=0, MB=0, SELCOP=1, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

or reg1 reg2 reg3 {
            co=000000,
            cop=0001,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                (MC=1, MR=0, SELA=1011, SELB=10000, MA=0, MB=0, SELCOP=10, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

not reg {
            co=000000,
            cop=0010,
            nwords=1,
            reg=reg(25,21),
            {
                (MC=1, MR=0, SELA=10101, MA=0, SELCOP=11, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

xor reg1 reg2 reg3 {
            co=000000,
            cop=0011,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                (MC=1, MR=0, SELA=1011, SELB=10000, MA=0, MB=0, SELCOP=100, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

add reg1 reg2 reg3 {
            co=000000,
            cop=1001,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                (MC=1, MR=0, SELA=1011, SELB=10000, MA=0, MB=0, SELCOP=1010, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

sub reg1 reg2 reg3 {
            co=000000,
            cop=1010,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                (MC=1, MR=0, SELB=1011, SELA=10000, MA=0, MB=0, SELCOP=1011, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

mul reg1 reg2 reg3 {
            co=000000,
            cop=1011,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                (MC=1, MR=0, SELA=1011, SELB=10000, MA=0, MB=0, SELCOP=1100, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0)
            }
}

div reg1 reg2 reg3 {
            co=000000,
            cop=1100,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            reg3=reg(15,11),
            {
                    # if (reg3 == 0)
                    (MC=1, MR=0, SELA=1011, MA=0, MB=11, SELCOP=1100, SELP=11, M7, C7),
                    (A0=0, B=0, C=110, MADDR=fpe1),
                    # reg1 = reg2 / reg3, go fetch
                    (MC=1, MR=0, SELB=1011, SELA=10000, MA=0, MB=0, SELCOP=1101, T6=1, SELC=10101, LC=1, SELP=11, M7, C7, A0=1, B=1, C=0),
              fpe1: # RT1 <- ExCode=1
                    (ExCode=1, T11, C4),
                    # csw_rt1(2)
                    (A0=0, B=1, C=0, MADDR=csw_rt1)
            }
}

srl reg1 reg2 val {
            co=010111,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            val=inm(5,0),
            {
                (SE=1, OFFSET=0, SIZE=110, T3=1, C4=1),
                (MC=1, MR=0, SELA=10000, SELB=10000, MA=0, MB=0, SELCOP=1, T6=1, SELC=10101),
         loop9: (A0=0, B=0, C=110, MADDR=bck2ftch),
                (MC=1, MR=0, SELA=10101, SELB=10101, MA=0, MB=0, SELCOP=101, T6=1, LC=1, SELC=10101),
                (MC=1, MR=0, MA=1, MB=11, SELCOP=1011, T6=1, C4=1, SELP=11, M7, C7),
                (A0=0, B=1, C=0, MADDR=loop9),
      bck9ftch: (A0=1, B=1, C=0)
            }
}


#
# MV/L*
#

move reg1 reg2 {
            co=000001,
            nwords=1,
            reg1=reg(25,21),
            reg2=reg(20,16),
            {
                (SELA=10000, T9, SELC=10101, LC, A0=1, B=1, C=0)
            }
}

li reg val {
            co=000010,
            nwords=1,
            reg=reg(25,21),
            val=inm(15,0),
            {
                (SE=1, OFFSET=0, SIZE=10000, SE=1, T3=1, LC=1, MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

liu reg val {
            co=111100,
            nwords=1,
            reg=reg(25,21),
            val=inm(15,0),
            {
                (SE=1, OFFSET=0, SIZE=10000, SE=0, T3=1, LC=1, MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

la  reg addr {
            co=000011,
            nwords=1,
            reg=reg(25,21),
            addr=address(15,0)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, LC=1, MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

la  reg addr {
            co=111111,
            nwords=2,
            reg=reg(25,21),
            addr=address(63,32)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, LC=1, MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

lw reg addr {
            co=000100,
            nwords=1,
            reg=reg(25,21),
            addr=address(15,0)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, C0=1),
                (TA=1, R=1, BW=11, M1=1, C1=1),
                (T1=1, LC=1, MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

lb reg1 (reg2) {
            co=100101,
            nwords=1,
            reg1 = reg(25,21),
            reg2 = reg(20,16),
            {
                (MR=0, SELA=10000, T9=1, C0),
                (TA=1, R=1, BW=00, M1=1, C1=1),
                (T1=1, LC=1, MR=0, SELC=10101, SE=1, A0=1, B=1, C=0)
            }
}

lbu reg1 (reg2) {
            co=101111,
            nwords=1,
            reg1 = reg(25,21),
            reg2 = reg(20,16),
            {
                (MR=0, SELA=10000, T9=1, C0),
                (TA=1, R=1, BW=00, M1=1, C1=1),
                (T1=1, LC=1, MR=0, SELC=10101, SE=0, A0=1, B=1, C=0)
            }
}

sw reg addr {
            co=000101,
            nwords=1,
            reg=reg(25,21),
            addr=address(15,0)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, C0=1),
                (MR=0, SELA=10101,    T9=1, M1=0, C1=1),
                (BW=11, TA=1, TD=1,     W=1,  A0=1, B=1, C=0)
            }
}

lb reg addr {
            co=001000,
            nwords=1,
            reg=reg(25,21),
            addr=address(15,0)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, C0=1),
                (TA=1, R=1, BW=00, SE=1, M1=1, C1=1),
                (T1=1, LC=1, MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

sb reg addr {
            co=001001,
            nwords=1,
            reg=reg(25,21),
            addr=address(15,0)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, C0=1),
                (MR=0, SELA=10101,    T9=1, M1=0, C1=1),
                (BW=0, TA=1, TD=1,     W=1,  A0=1, B=1, C=0)
            }
}


#
# IN/OUT
#

in reg val {
            co=001010,
            nwords=1,
            reg=reg(25,21),
            val=inm(15,0),
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, C0=1),
                (TA=1, IOR=1, BW=11, M1=1, C1=1),
                (T1=1, LC=1,  MR=0, SELC=10101, A0=1, B=1, C=0)
            }
}

out reg val {
            co=001011,
            nwords=1,
            reg=reg(25,21),
            val=inm(15,0),
            {
                (SE=0, OFFSET=0,   SIZE=10000,   T3=1, C0=1),
                (MR=0, SELA=10101, T9=1,         M1=0, C1=1),
                (TA=1, TD=1,       IOW=1, BW=11, A0=1, B=1, C=0)
            }
}


#
# b*
#

b offset {
            co=001100,
            nwords=1,
            offset=address(15,0)rel,
            {
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0)
            }
}

beq reg reg offset {
            co=001101,
            nwords=1,
            reg=reg(25,21),
            reg=reg(20,16),
            offset=address(15,0)rel,
            {
                (T8, C5),
                (SELA=10101, SELB=10000, MC=1, SELCOP=1011, SELP=11, M7, C7),
                (A0=0, B=1, C=110, MADDR=bck2ftch),
                (T5, M7=0, C7),
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0),
      bck2ftch: (T5, M7=0, C7),
                (A0=1, B=1, C=0)
            }
}

bne reg reg offset {
            co=001110,
            nwords=1,
            reg=reg(25,21),
            reg=reg(20,16),
            offset=address(15,0)rel,
            {
                (T8, C5),
                (SELA=10101, SELB=10000, MC=1, SELCOP=1011, SELP=11, M7, C7),
                (A0=0, B=0, C=110, MADDR=bck3ftch),
                (T5, M7=0, C7),
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0),
      bck3ftch: (T5, M7=0, C7),
                (A0=1, B=1, C=0)
            }
}

bge reg reg offset {
            co=001111,
            nwords=1,
            reg=reg(25,21),
            reg=reg(20,16),
            offset=address(15,0)rel,
            {
                (T8, C5),
                (SELA=10101, SELB=10000, MC=1, SELCOP=1011, SELP=11, M7, C7),
                (A0=0, B=0, C=111, MADDR=bck4ftch),
                (T5, M7=0, C7),
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0),
      bck4ftch: (T5, M7=0, C7),
                (A0=1, B=1, C=0)
            }
}

blt reg reg offset {
            co=010000,
            nwords=1,
            reg=reg(25,21),
            reg=reg(20,16),
            offset=address(15,0)rel,
            {
                (T8, C5),
                (SELA=10101, SELB=10000, MC=1, SELCOP=1011, SELP=11, M7, C7),
                (A0=0, B=1, C=111, MADDR=bck5ftch),
                (T5, M7=0, C7),
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0),
      bck5ftch: (T5, M7=0, C7),
                (A0=1, B=1, C=0)
            }
}

bgt reg reg offset {
            co=010001,
            nwords=1,
            reg=reg(25,21),
            reg=reg(20,16),
            offset=address(15,0)rel,
            {
                (T8, C5),
                (SELA=10101, SELB=10000, MC=1, SELCOP=1011, SELP=11, M7, C7),
                (A0=0, B=0, C=111, MADDR=bck6ftch),
                (A0=0, B=0, C=110, MADDR=bck6ftch),
                (T5, M7=0, C7),
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0),
      bck6ftch: (T5, M7=0, C7),
                (A0=1, B=1, C=0)
            }
}

ble reg reg offset {
            co=010010,
            nwords=1,
            reg=reg(25,21),
            reg=reg(20,16),
            offset=address(15,0)rel,
            {
                (T8, C5),
                (SELA=10101, SELB=10000, MC=1, SELCOP=1011, SELP=11, M7, C7),
                (A0=0, B=0, C=111, MADDR=ble_ys),
                (A0=0, B=0, C=110, MADDR=ble_ys),
                (T5, M7=0, C7),
                (A0=1, B=1, C=0),
        ble_ys: (T5, M7=0, C7),
                (T2, C4),
                (SE=1, OFFSET=0, SIZE=10000, T3, C5),
                (MA=1, MB=1, MC=1, SELCOP=1010, T6, C2, A0=1, B=1, C=0)
            }
}


#
# j*
#

j addr {
            co=010011,
            nwords=1,
            addr=address(15,0)abs,
            {
                (SE=0, OFFSET=0, SIZE=10000, T3=1, M2=0, C2=1, A0=1, B=1, C=0)
            }
}

jal addr {
            co=010100,
            nwords=1,
            addr=address(15,0)abs,
            {
                (T2, SELC=11111, MR=1, LC),
                (SE=0, OFFSET=0, SIZE=10000, T3=1, M2=0, C2=1, A0=1, B=1, C=0)
            }
}

jr reg1 {
            co=010101,
            nwords=1,
            reg1=reg(25,21),
            {
                (SELA=10101, T9=1, C2=1, A0=1, B=1, C=0)
            }
}


#
# Misc
#

nop {
        co=010110,
        nwords=1,
        {
                (A0=1, B=1, C=0)
        }
}


registers
{
        0=$zero,
        1=$at,
        2=$v0,
        3=$v1,
        4=$a0,
        5=$a1,
        6=$a2,
        7=$a3,
        8=$t0,
        9=$t1,
        10=$t2,
        11=$t3,
        12=$t4,
        13=$t5,
        14=$t6,
        15=$t7,
        16=$s0,
        17=$s1,
        18=$s2,
        19=$s3,
        20=$s4,
        21=$s5,
        22=$s6,
        23=$s7,
        24=$t8,
        25=$t9,
        26=$k0,
        27=$k1,
        28=$gp,
        29=$sp (stack_pointer),
        30=$fp,
        31=$ra
}

